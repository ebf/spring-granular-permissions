plugins {
    id 'java-library'
    id 'groovy'
    id 'idea'
    id 'jacoco'
    id 'maven-publish'

    id 'io.freefair.lombok' version '8.4'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.github.hierynomus.license'  version '0.16.1'
    id 'org.hibernate.build.maven-repo-auth' version '3.0.4' apply(false)
}

// do not apply the plugin when the build is running on Github actions as there we do not have
// an m2-settings.xml file. This plugin would throw an NPE when credentials could not be resolved
if (System.getenv('CI') != 'github') {
    apply plugin: 'org.hibernate.build.maven-repo-auth'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = uri("https://repository.hosting.ebf.de/nexus/repository/releases/") }
}

group = "com.ebf"
archivesBaseName = "spring-granular-permissions"
version = "3.1.0"

jar {
    manifest {
        attributes 'Implementation-Title': 'Spring Granular Permissions',
                'Implementation-Version': archiveVersion
    }
}

java {
    withSourcesJar()
}

idea {
    module {
        outputDir = file("build/classes/main")
        testOutputDir = file("build/classes/test")
    }
}

ext {
    set('springVersion', '3.2.2')
}

dependencies {
    compileOnly('org.springframework.boot:spring-boot-starter-data-jpa')
    compileOnly('org.springframework.boot:spring-boot-starter-security')

    /* Test Database implementation */
    testImplementation('com.h2database:h2')

    /* Spring Boot Test dependencies */
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.springframework.boot:spring-boot-starter-web')
    testImplementation('org.springframework.boot:spring-boot-starter-data-rest')
    testImplementation('org.springframework.boot:spring-boot-starter-data-jpa')
    testImplementation('org.springframework.boot:spring-boot-starter-security')

    /* Spock Test dependencies */
    testImplementation('org.spockframework:spock-spring:2.4-M1-groovy-4.0')

    /* Test utilities */
    testImplementation('org.apache.groovy:groovy-json:4.0.18')
}

dependencyManagement {
    generatedPomCustomization {
        enabled = false
    }

    imports {
        mavenBom("org.springframework.boot:spring-boot-dependencies:${springVersion}")
    }
}

license {
    header = file('header.txt')
    exclude "LICENSE.txt"
}

test {
    useJUnitPlatform()

    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        csv.required = false
        xml.required = true
        html.required = true
    }

    finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.80 // at least 80% coverage is required
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = 'Spring Granular Permissions'
                description = "Granular permission library for spring boot"
                url = 'https://github.com/ebf/spring-granular-permissions'
                packaging = 'jar'

                organization {
                    name = 'EBF GmbH'
                    url = 'https://ebf.com'
                }

                developers {
                    developer {
                        id = 'EBF'
                        name = 'EBF'
                        email = 'dev@ebf.com'
                        organization = 'EBF GmbH'
                        organizationUrl = 'https://ebf.com'
                    }

                    developers {
                        developer {
                            id = 'iznenad'
                            name = 'Nenad Nikolic'
                            email = 'nenad.nikolic@ebf.de'
                            organization = 'EBF GmbH'
                            organizationUrl = 'https://ebf.com'
                        }
                    }
                }

                issueManagement {
                    system = 'Github'
                    url = 'https://github.com/ebf/spring-granular-permissions/issues'
                }

                scm {
                    developerConnection = 'scm:git:ssh://git@github.com/ebf/spring-granular-permissions.git'
                    connection = 'scm:git:git://github.com/ebf/spring-granular-permissions.git'
                    url = 'https://github.com/ebf/spring-granular-permissions'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = 'ebf-releases-deployment'
            url = 'https://repository.hosting.ebf.de/nexus/content/repositories/releases/'
        }
    }
}