plugins {
    id 'java-library'
    id 'groovy'
    id 'idea'
    id 'jacoco'
    id 'maven-publish'

    id 'io.freefair.lombok' version '5.0.1'
    id 'com.github.hierynomus.license'  version '0.16.1'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = uri("https://repository.hosting.ebf.de/nexus/repository/releases/") }
}

group = "de.ebf"
archivesBaseName = "spring-granular-permissions"
version = "2.0.0"

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Spring Granular Permissions',
                'Implementation-Version': archiveVersion
    }
}

java {
    withSourcesJar()
}

idea {
    module {
        outputDir = file("build/classes/main")
        testOutputDir = file("build/classes/test")
    }
}

dependencies {
    compileOnly('org.springframework.boot:spring-boot-starter-data-jpa:2.6.2')
    compileOnly('org.springframework.boot:spring-boot-starter-security:2.6.2')

    /* Test Database implementation */
    testImplementation('com.h2database:h2:2.0.204')

    /* Spring and Spock Test dependencies */
    testImplementation('org.springframework.boot:spring-boot-starter-test:2.6.2')
    testImplementation('org.spockframework:spock-core:2.0-groovy-3.0')
    testImplementation('org.spockframework:spock-spring:2.0-groovy-3.0')

    /* Test utilities */
    testImplementation('org.codehaus.groovy:groovy-json:3.0.9')

    /* Spring Boot Test dependencies */
    testImplementation('org.springframework.boot:spring-boot-starter-web:2.6.2')
    testImplementation('org.springframework.boot:spring-boot-starter-data-rest:2.6.2')
    testImplementation('org.springframework.boot:spring-boot-starter-data-jpa:2.6.2')
    testImplementation('org.springframework.boot:spring-boot-starter-security:2.6.2')
}

license {
    header = file('header.txt')
    exclude "LICENSE.txt"
}

test {
    useJUnitPlatform()

    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        csv.required = false
        xml.required = true
        html.required = true
    }

    finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.80 // at least 80% coverage is required
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = 'Spring Granular Permissions'
                description = "Granular permission library for spring boot"
                url = 'https://github.com/ebf/spring-granular-permissions'
                packaging = 'jar'

                organization {
                    name = 'EBF GmbH'
                    url = 'https://ebf.com'
                }

                developers {
                    developer {
                        id = 'EBF'
                        name = 'EBF'
                        email = 'dev@ebf.com'
                        organization = 'EBF GmbH'
                        organizationUrl = 'https://ebf.com'
                    }

                    developers {
                        developer {
                            id = 'iznenad'
                            name = 'Nenad Nikolic'
                            email = 'nenad.nikolic@ebf.de'
                            organization = 'EBF GmbH'
                            organizationUrl = 'https://ebf.com'
                        }
                    }
                }

                issueManagement {
                    system = 'Github'
                    url = 'https://github.com/ebf/spring-granular-permissions/issues'
                }

                scm {
                    developerConnection = 'scm:git:ssh://git@github.com/ebf/spring-granular-permissions.git'
                    connection = 'scm:git:git://github.com/ebf/spring-granular-permissions.git'
                    url = 'https://github.com/ebf/spring-granular-permissions'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = 'ebf-releases-deployment'
            url = 'https://repository.hosting.ebf.de/nexus/content/repositories/releases/'
        }
    }
}
